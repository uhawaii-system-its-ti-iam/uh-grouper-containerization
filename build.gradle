import com.google.cloud.tools.jib.api.Containerizer
import com.google.cloud.tools.jib.api.DockerDaemonImage
import com.google.cloud.tools.jib.api.Jib
import com.google.cloud.tools.jib.api.AbsoluteUnixPath

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.cloud.tools:jib-core:0.13.1'
    }
}

plugins {
    id 'java'
}

group 'edu.hawaii'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "javax.servlet:javax.servlet-api:3.1.0"
    compileOnly "org.slf4j:slf4j-api:1.7.21"

    compileOnly "edu.internet2.middleware.grouper:grouperClient:2.5.22"

    compile "org.pac4j:jee-pac4j:5.0.0"
    compile "org.pac4j:pac4j-cas:4.0.0"

    compileOnly "edu.internet2.middleware.grouper:grouper-ui:2.5.22"
    compileOnly "org.apache.tomcat:tomcat-catalina:8.5.50"
}

jar {
    archiveBaseName = 'grouper-pac4j'
}

task createDockerImage {
    group = 'scalding'
    dependsOn 'jar'
    doLast {
        Jib.from('docker://scalding/grouper-abused-ui:2.5.22')
            .addLayer(tasks.getByName('jar').outputs.files.collect { it.toPath() }, '/opt/grouper/grouperWebapp/WEB-INF/lib')
            .addLayer(configurations.runtime.files.collect { it.toPath() }, '/opt/grouper/grouperWebapp/WEB-INF/lib')
            .addLayer([file('/src/main/docker/container-files/javax.servlet.ServletContainerInitializer').toPath()], '/opt/grouper/grouperWebapp/WEB-INF/classes/META-INF/services')
            .setWorkingDirectory(AbsoluteUnixPath.get('/opt/grouper/grouperWebapp/WEB-INF/bin'))
            .setEntrypoint('/opt/tomee/bin/catalina.sh', 'run')
            .containerize(Containerizer.to(DockerDaemonImage.named('scalding/grouper-abused-ui-pac4j')))
    }
}
